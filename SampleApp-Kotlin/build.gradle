buildscript {
    ext.kotlin_version = '1.1.50'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 26
    buildToolsVersion '24.0.1'

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 26
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    // Only for SampleApp.
    // It is failing because the okio version and okhttp is not found, which is expected.
    lintOptions {
        abortOnError false
    }


    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }

        release{
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                output.outputFile = new File(outputFile.parent, "paypal-sample-app-${variant.name}.apk")
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    if (parent != null) {
        compile project(path: ':androidSDK', configuration: 'generalDebug')
    } else {
        compile('com.paypal.sdk:paypal-android-sdk:2.16.0')
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
